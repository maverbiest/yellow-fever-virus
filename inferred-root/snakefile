# Set the parameters
REFERENCE_ACCESSION =   "<your virus accession>"
MIN_DATE =              "1990-01-01" # all sequences before then are excluded
MIN_LENGTH =            "6000" #TODO: set lower to include shorter fragments
MAX_SEQS =              "1000" #TODO: set to 10000 for testing
ID_FIELD=               "accession" # either "accession" or "strain", used for meta-id-column in augur

OUTGROUP = ["MT081371.1", "DQ201177.1", "MT081370.1"] # EV-D111, EV-D70 and EV-D94
# OUTGROUP = REFERENCE_ACCESSION

# Set the paths (make sure they are the same as those in the top-level Snakefile)
EXCLUDE =               "../resources/exclude.txt"
SEQUENCES =             "../data/sequences.fasta"
METADATA =              "../results/metadata.tsv"
INCLUDE =               "../results/include.txt"
INFERRED_ANCESTOR =     "resources/inferred-root.fasta"

rule all_sub:
    input:
        seqs = INFERRED_ANCESTOR
    shell:
        """
        cp {input.seqs} ../{input.seqs}
        """

rule index_sequences:
    message:
        """
        Creating an index of sequence composition for filtering
        """
    input:
        sequences = SEQUENCES,
    output:
        sequence_index = "results/sequence_index.tsv"
    shell:
        """
        augur index \
            --sequences {input.sequences} \
            --output {output.sequence_index}
        """

rule filter:
    """
    Exclude sequences from before {MIN_DATE} and subsample to {MAX_SEQS} sequences.
    Only take sequences longer than {MIN_LENGTH}
    """
    input:
        sequences = SEQUENCES,
        sequence_index = rules.index_sequences.output.sequence_index,
        metadata = METADATA,
        include = INCLUDE,
        exclude = EXCLUDE,

    output:
        filtered_sequences = "results/filtered_sequences.fasta",
        filtered_metadata = "results/filtered_metadata.tsv",
        strains = "results/tree_strains.txt",

    params: 
        min_date="" if MIN_DATE == "" else "--min-date " + MIN_DATE,
        min_length="" if MIN_LENGTH == "" else "--min-length " + MIN_LENGTH,
        max_seqs=MAX_SEQS,
        categories = "country year", #TODO: add subsampling per category?
        strain_id_field = ID_FIELD,
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --sequence-index {input.sequence_index} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id_field} \
            {params.min_length} \
            {params.min_date} \
            --include {input.include} \
            --exclude {input.exclude} \
            --subsample-max-sequences {params.max_seqs} \
            --output-sequences {output.filtered_sequences} \
            --output-metadata {output.filtered_metadata} \
            --output-strains {output.strains}

        """

rule join_fastas:
    input:
        outgroup= expand("resources/outgroup/{o}.fasta", o = OUTGROUP),
        ingroup= rules.filter.output.filtered_sequences,
    output: "results/joined.fasta"
    shell: 
        """
        cat {input.outgroup} {input.ingroup} > tmp.fasta
        seqkit rmdup -n tmp.fasta -o {output}
        rm tmp.fasta
        """


rule align_fastas:
    input: rules.join_fastas.output
    params: 
    output: "results/aligned.fasta"
    threads: workflow.cores
    shell: "mafft --thread {threads} --auto {input} > {output}"

rule tree:
    message:
        """
        Creating a maximum likelihood tree
        """
    input:
        rules.align_fastas.output
    output: 
        "results/tree.nwk"
    threads: workflow.cores
    shell:
        """
        augur tree \
            --alignment {input} \
            --nthreads {threads}\
            --output {output} \
        """

rule pick_ancestral_sequence:
    input:
        tree = rules.tree.output,
        alignment = rules.align_fastas.output,
        sequences = rules.filter.output.filtered_sequences,
    params:
        outgroup = OUTGROUP,
        reference = REFERENCE_ACCESSION,
    output: 
        INFERRED_ANCESTOR
    shell: """
        python ../scripts/pick_ancestral_sequence.py \
            {input.tree} \
            {input.alignment} \
            {input.sequences} \
            "{params.outgroup}"\
            {params.reference} \
            {output}
        """

rule clean:
    shell:
        """
        rm ../resources/inferred-root.fasta resources/inferred-root.fasta
        rm -r results/*
        """